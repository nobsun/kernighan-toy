---
title: "Haskellã§ã‚«ãƒ¼ãƒ‹ãƒãƒ³å…ˆç”Ÿã®ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã‚’æ›¸ã"
emoji: "ðŸ‡­"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["Haskell","é–¢æ•°ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°"]
published: false
---

# ã¯ã˜ã‚ã«

[ã€Žãƒ‡ã‚£ã‚¸ã‚¿ãƒ«ä½œæ³• â€” ã‚«ãƒ¼ãƒ‹ãƒãƒ³å…ˆç”Ÿã®ã€Œæƒ…å ±ã€æ•™å®¤ã€](https://amzn.to/386fNYZ)^[åŽŸæ›¸ã«ã¯æ”¹è¨‚ç‰ˆãŒã‚ã‚Šã€ç¿»è¨³ã‚‚ã•ã‚Œã¦ã„ã¾ã™ã€‚[ã€Žæ•™é¤Šã¨ã—ã¦ã®ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã‚µã‚¤ã‚¨ãƒ³ã‚¹è¬›ç¾© â€” ä»Šã“ãçŸ¥ã£ã¦ãŠãã¹ãã€Œãƒ‡ã‚¸ã‚¿ãƒ«ä¸–ç•Œã€ã®åŸºç¤ŽçŸ¥è­˜ã€](https://amzn.to/34M2gUj)]ã«ã€CPUã®å‹•ä½œã®ã‚ˆã†ã™ã‚’èª¬æ˜Žã™ã‚‹ãŸã‚ã®ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãŒç™»å ´ã—ã¾ã™ã€‚ã‚«ãƒ¼ãƒ‹ãƒãƒ³å…ˆç”Ÿã®ã‚µã‚¤ãƒˆã§ã¯ã€[ã‚·ãƒŸãƒ¥ãƒ¬ãƒ¼ã‚¿ãƒ¼](https://kernighan.com/toysim.html)ãŒæä¾›ã•ã‚Œã¦ã„ã¾ã™ã€‚ã“ã®è¨˜äº‹ã§ã¯ã€GUIã®ãªã„å˜ç´”ãªãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®ã‚·ãƒŸãƒ¥ãƒ¬ãƒ¼ã‚¿ã‚’Haskellã§æ›¸ã„ã¦ã¿ã¾ã™ã€‚å‘½ä»¤åˆ—ã®é †æ¬¡å®Ÿè¡Œã¨ã„ã†ã€ã„ã‹ã«å‘½ä»¤çš„ãªæŒ¯èˆžã„ã‚’é–¢æ•°çš„ã«è€ƒãˆã‚‹ã¨ã„ã†è©¦ã¿ã§ã™ã€‚

:::details ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«å®£è¨€ã¨ã‚¤ãƒ³ãƒãƒ¼ãƒˆå®£è¨€
```haskell
module Toysim where

import qualified Data.Array as A
import Data.Char
import Data.List
import Data.Maybe
```
:::

# ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿

ã€Žãƒ‡ã‚£ã‚¸ã‚¿ãƒ«ä½œæ³•ã€ã«ã¯ã€ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®æ§‹æˆã¨å‹•ä½œã«ã¤ã„ã¦ä»¥ä¸‹ã®ã‚ˆã†ã«è¨˜è¿°ã•ã‚Œã¦ã„ã¾ã™ã€‚

> ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã«ã¯å‘½ä»¤ã¨ãƒ‡ãƒ¼ã‚¿ã‚’æ ¼ç´ã™ã‚‹ãƒ¡ãƒ¢ãƒªãŒã‚ã‚Šã€ãã‚Œã«åŠ ãˆã¦**ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿**ã¨ã„ã†åå‰ã®ã€æ•°å€¤ã‚’1ã¤ä¿æŒã§ãã‚‹è¨˜æ†¶é ˜åŸŸãŒå‚™ã‚ã£ã¦ã„ã¾ã™ã€‚
> ... ä¸­ç•¥ ...
> ãƒ¡ãƒ¢ãƒªã®ãã‚Œãžã‚Œã®å ´æ‰€ã«ã¯æ•°å€¤ãŒ1å€‹ã€ã¾ãŸã¯å‘½ä»¤ãŒ1å€‹ä¿æŒã•ã‚Œã¦ã„ã‚‹ã‚‚ã®ã¨ã—ã¾ã™ã€‚ã§ã™ã‹ã‚‰ã€ãƒ¡ãƒ¢ãƒªä¸­ã«ã¯å‘½ä»¤ãŒã„ãã¤ã‹ã¨ãƒ‡ãƒ¼ã‚¿é …ç›®ãŒã„ãã¤ã‹å…¥ã£ã¦ã„ã¦ã€ã“ã‚Œã‚‰ã‚’åˆã‚ã›ãŸã‚‚ã®ãŒãƒ—ãƒ­ã‚°ãƒ©ãƒ ã ã¨ã„ã†ã“ã¨ã«ãªã‚Šã¾ã™ã€‚
> ... ä¸­ç•¥ ...
> å‹•ä½œä¸­ã«ã¯ã€CPUã¯æœ€åˆã®ãƒ¡ãƒ¢ãƒªç•ªåœ°ã‹ã‚‰å§‹ã‚ã¦ã€æ¬¡ã®ã‚ˆã†ãªå˜ç´”ãªã‚µã‚¤ã‚¯ãƒ«ã‚’ç¹°ã‚Šè¿”ã—ã¾ã™ã€‚
> - **ãƒ•ã‚§ãƒƒãƒï¼ˆå–ã‚Šå‡ºã—ï¼‰**ï¼šãƒ¡ãƒ¢ãƒªã‹ã‚‰æ¬¡ã®å‘½ä»¤ã‚’å–ã‚Šå‡ºã™
> - **ãƒ‡ã‚³ãƒ¼ãƒ‰ï¼ˆè§£èª­ï¼‰**ï¼šãã®å‘½ä»¤ãŒä½•ã‚’ã™ã‚‹ã‹ã‚’åˆ¤æ–­ã™ã‚‹
> - **å®Ÿè¡Œ**ï¼šå‘½ä»¤ã®å‹•ä½œã‚’å®Ÿè¡Œã—ã¦ã€ãƒ•ã‚§ãƒƒãƒã«æˆ»ã‚‹

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®å‹•ä½œã¯ã€å‘½ä»¤çš„ç–‘ä¼¼è¨€èªžã§è¡¨ç¾ã™ã‚‹ã¨

```
toy.init(program);
while (!toy.is_stopped()) {
    toy.fetch();
    toy.decode();
    toy.execute();
}
```

ã®ã‚ˆã†ã«ãªã‚‹ã§ã—ã‚‡ã†ã€‚ã§ã¯é–¢æ•°çš„ã«ã¯ã©ã®ã‚ˆã†ãªè¡¨ç¾ã«ãªã‚‹ã§ã—ã‚‡ã†ã‹ã€‚

## ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®å‹•ä½œã®è¡¨ç¾

1ã¤ã®å‘½ä»¤ã®ã€Œå®Ÿè¡Œã€ã‚’ã€å®Ÿè¡Œå‰ã®ã€ŒçŠ¶æ…‹ã€ã‹ã‚‰å®Ÿè¡Œå¾Œã®ã€ŒçŠ¶æ…‹ã€ã¸ã®çŠ¶æ…‹é·ç§»ã‚’è¡¨ã™é–¢æ•°ã®é©ç”¨ã¨ã—ã¦è¡¨ç¾ã§ãã¾ã™ã€‚

```haskell
execute :: Instruction -> ToyState -> ToyState
execute instr state = instr state
```

ã™ãªã‚ã¡ã€å‘½ä»¤ã¯ã€çŠ¶æ…‹é·ç§»é–¢æ•°ã¨ã„ã†ã‚ã‘ã§ã™ã€‚

```haskell
type Instruction = ToyState -> ToyState
```

ãã†ã™ã‚‹ã¨ã€ãƒ•ã‚§ãƒƒãƒã¨ãƒ‡ã‚³ãƒ¼ãƒ‰ã¯ä»¥ä¸‹ã®ã‚ˆã†ãªåž‹ã‚’æŒã¤é–¢æ•°ã«ãªã‚‹ã§ã—ã‚‡ã†ã€‚

```haskell
fetch :: ToyState -> Code
decode :: Code -> Instruction
```

ã•ã‚‰ã«ã€ãƒ•ã‚§ãƒƒãƒ-ãƒ‡ã‚³ãƒ¼ãƒ‰-å®Ÿè¡Œã®1ã‚µã‚¤ã‚¯ãƒ«åˆ†ã‚’1æ®µã®çŠ¶æ…‹é·ç§»é–¢æ•°ã ã¨ã™ã‚‹ã¨ã€

```haskell
step :: ToyState -> ToyState
step state = execute (decode (fetch state)) state
```

ãã—ã¦ã€1æ®µã®çŠ¶æ…‹é·ç§»é–¢æ•°`step`ãŒã‚ã‚Œã°ã€ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ä¸Šã§ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®å®Ÿè¡Œã¯ã€åˆæœŸçŠ¶æ…‹ã‹ã‚‰åœæ­¢çŠ¶æ…‹ã¾ã§ã®çŠ¶æ…‹é·ç§»åˆ—ã¨ã—ã¦è¡¨ç¾ã§ãã¾ã™ã€‚

```haskell
executeProgram :: ToyState -> [ToyState]
executeProgram state = state : rests
    where
        rests | isFinal state = []
              | otherwise     = executeProgram (step state)
```

## ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®çŠ¶æ…‹

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®ã€ŒçŠ¶æ…‹ã€ã¯ã©ã®ã‚ˆã†ãªæ§‹æˆã«ãªã£ã¦ã„ã‚Œã°ã‚ˆã„ã§ã—ã‚‡ã†ã‹ã€‚

ç¬¬1ã«ã€ãƒ•ã‚§ãƒƒãƒ`fetch`ã«ã‚ˆã£ã¦ç¾åœ¨ã®çŠ¶æ…‹ã‹ã‚‰ã€å®Ÿè¡Œã™ã¹ãå‘½ä»¤ã‚³ãƒ¼ãƒ‰ã‚’å–ã‚Šå‡ºã›ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚å®Ÿè¡Œ`execute`ã«ã‚ˆã£ã¦æ§‹æˆã•ã‚Œã‚‹æ¬¡ã®çŠ¶æ…‹ã«ã¯æ¬¡ã«å®Ÿè¡Œã™ã¹ãå‘½ä»¤ã‚³ãƒ¼ãƒ‰ãŒå«ã¾ã‚Œã¦ã„ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚ã—ãŸãŒã£ã¦ã€ã€ŒçŠ¶æ…‹ã€ã«ã¯ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’å«ã‚€ã€Œãƒ¡ãƒ¢ãƒªã€ãŒå¿…è¦ã§ã™ã€‚
ç¬¬2ã«ã€1ã¤ã®å‘½ä»¤ã‚’å®Ÿè¡Œã—ã¦å¾—ã‚‰ã‚ŒãŸå€¤ã‚’æ¬¡ã«ä¼ãˆã‚‹ãŸã‚ã«ã€ã€Œã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã€ãŒå¿…è¦ã§ã™ã€‚
ç¬¬3ã«ã€å¤–éƒ¨ã‹ã‚‰ã®ã€Œå…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã€ãŒå¿…è¦ã§ã™
ç¬¬4ã«ã€å¤–éƒ¨ã¸ã®ã€Œå‡ºåŠ›ã€ãŒå¿…è¦ã§ã™

ã¨ã„ã†ã‚ã‘ã§ã€ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®çŠ¶æ…‹`ToyState`ã¯ã€ãƒ¡ãƒ¢ãƒª`Memory`ã€ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿`Accumulator`ã€å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«`Channel`ã€å‡ºåŠ›`Output`ã®4ã¤çµ„ã§è¡¨ç¾ã—ã¾ã™ã€‚

```haskell
type ToyState = (Memory, Accumulator, Channel, Output)
```

### ãƒ¡ãƒ¢ãƒªã®è¡¨ç¾

ãƒ¡ãƒ¢ãƒªã®å†…å®¹`Content`ã¯ã€å‘½ä»¤ã‚³ãƒ¼ãƒ‰`Code`ã¾ãŸã¯ãƒ‡ãƒ¼ã‚¿`Data`ã§ã™ã€‚å€‹ã€…ã®å‘½ä»¤ã‚„ãƒ‡ãƒ¼ã‚¿ã¯ãƒ©ãƒ™ãƒ«ã‚’ä½¿ã£ã¦å‚ç…§ã™ã‚‹ã“ã¨ãŒã‚ã‚‹ã®ã§ã€ãƒ¡ãƒ¢ãƒªã¯ãƒ©ãƒ™ãƒ«ä»˜ãå†…å®¹`(Label, Content)`ã®ä¸¦ã³ã¨ã—ã¦è¡¨ç¾ã—ã¾ã—ã‚‡ã†ã€‚ä¸¦ã³ã®è¡¨ç¾ã¨ã—ã¦ã¯ã€Haskellã§ã¯ãƒªã‚¹ãƒˆã‚’ç”¨ã„ã‚‹ã®ãŒä¸€èˆ¬çš„ã§ã™^[ãƒªã‚¹ãƒˆã«ã‚ˆã‚‹è¡¨ç¾ã§ã¯ã€ä»»æ„ã®è¦ç´ ã¸ã®ã‚¢ã‚¯ã‚»ã‚¹ãŒã€é€šå¸¸ã®RAMã«æœŸå¾…ã•ã‚Œã‚‹$O(1)$ã«ã¯ãªã‚‰ãªã„ã“ã¨ã«æ³¨æ„ã—ã¦ãã ã•ã„ã€‚]ã€‚ãƒªã‚¹ãƒˆã§ã‚ã‚Œã°ã€ãƒ•ã‚§ãƒƒãƒã®ã¨ãã«ç¾åœ¨ã®ãƒ¡ãƒ¢ãƒªã®å…ˆé ­ã‚’å–ã‚Šå‡ºã—ã€æ¬¡ã®çŠ¶æ…‹ã‚’æ§‹æˆã™ã‚‹ã¨ãã¯ã€å…ˆé ­ã‚ˆã‚Šå¾Œã‚ã®éƒ¨åˆ†ã‚’æ¬¡ã®ãƒ¡ãƒ¢ãƒªã¨ã™ã‚Œã°ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®é †æ¬¡å®Ÿè¡ŒãŒè¡¨ç¾ã§ãã¾ã™ã€‚å‰æ–¹ã¸ã®ã‚¸ãƒ£ãƒ³ãƒ—å‘½ä»¤ã«å¯¾å¿œã—ãªã‘ã‚Œã°ãªã‚‰ãªã„ã®ã§ã€å¾ªç’°ãƒªã‚¹ãƒˆ`CyclicList a`ã‚’ä½¿ã£ã¦è¡¨ç¾ã™ã‚‹ã“ã¨ã«ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
type Memory = CyclicList (Label, Content)

type CyclicList a = (Size, [a])
type Size = Int

listToCyclicList :: [a] -> CyclicList a
listToCyclicList [] = error "empty"
listToCyclicList xs = (length xs, cycle xs)

type Label = String
data Content
    = Code Code
    | Data Data

type Code = (Operator, Operand)
type Data = Int
```

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®ã‚³ãƒ¼ãƒ‰`Code`ã¯ã‚ªãƒšãƒ¬ãƒ¼ã‚¿`Operator`ã¨ã‚ªãƒšãƒ©ãƒ³ãƒ‰`Operand`ã¨ã®ãƒšã‚¢ã§è¡¨ç¾ã—ã¾ã™ã€‚ã‚ªãƒšãƒ¬ãƒ¼ã‚¿ã¯[ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®ã‚·ãƒŸãƒ¥ãƒ¬ãƒ¼ã‚¿ãƒ¼ã‚µã‚¤ãƒˆ](https://kernighan.com/toysim.html)ã«ã‚ˆã‚Œã°ã€ä»¥ä¸‹ã®10ç¨®é¡žã‚ã‚Šã¾ã™ã€‚ã‚ªãƒšãƒ©ãƒ³ãƒ‰ã¯æ•°å€¤ã‹ãƒ©ãƒ™ãƒ«ã‹ãã‚Œã¨ã‚‚ç„¡ã—ã®3ç¨®é¡žã§ã™ã€‚
 
```haskell
data Operator
    = GET
    | PRINT
    | STOP
    | LOAD
    | STORE
    | ADD
    | SUB
    | GOTO
    | IFPOS
    | IFZERO
    deriving (Eq, Ord, Enum, Show, Read)

data Operand
    = None
    | Number Int
    | Label Label
    deriving (Eq, Show)
```

### ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®è¡¨ç¾

ã“ã¡ã‚‰ã¯å˜ç´”ã«æ•°å€¤ã§è¡¨ç¾ã§ãã¾ã™ã€‚

```haskell
type Accumulator = Int
```

### å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã®è¡¨ç¾

ãƒ¦ãƒ¼ã‚¶ãƒ¼å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«`Channel`ã¯0å€‹ä»¥ä¸Šã®å…¥åŠ›`Input`åˆ—ã®ä¸¦ã³`[Intput]`ã§è¡¨ç¾ã—ã¾ã—ã‚‡ã†ã€‚ã“ã‚Œã¯å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã¨ã—æ©Ÿèƒ½ã—ã€å…¥åŠ›å‘½ä»¤ã®å®Ÿè¡Œã«ã‚ˆã‚Šã€å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã®å…ˆé ­ã®è¦ç´ ï¼ˆæ–‡å­—åˆ—ï¼‰ãŒæ•°å€¤ã¨ã—ã¦è§£é‡ˆã•ã‚Œã¦ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã«ãªã‚Šã¾ã™ã€‚æ¬¡ã®çŠ¶æ…‹ã‚’æ§‹æˆã™ã‚‹å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã¯ç¾åœ¨ã®å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«ã®å…ˆé ­ã‚ˆã‚Šå¾Œã‚ã®éƒ¨åˆ†ã¨ã™ã‚Œã°ã‚ˆã„ã§ã—ã‚‡ã†ã€‚

```haskell
type Channel = [Input]
type Input = String
```

### å‡ºåŠ›ã®è¡¨ç¾

å‡ºåŠ›å‘½ä»¤ãŒå®Ÿè¡Œã•ã‚ŒãŸçµæžœã¨ã—ã¦æ§‹æˆã•ã‚Œã‚‹æ¬¡ã®çŠ¶æ…‹ã«å‡ºåŠ›æ–‡å­—åˆ—ãŒå…¥ã‚Šã¾ã™ã€‚

```haskell
type Output = String
```

### `fetch`

`ToyState`ã®å®šç¾©ãŒã§ããŸã®ã§ã€`fetch`ã‚’å®šç¾©ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
fetch ((_,mem),_,_,_) = case mem of
    (_,Code code):_ -> code
```

### `decode`

`decode`ã¯ã‚³ãƒ¼ãƒ‰ã‹ã‚‰å¯¾å¿œã—ãŸã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³ã¸ã®é–¢æ•°ãªã®ã§ã€ä»¥ä¸‹ã®ã‚ˆã†ã«å®šç¾©ã§ãã¾ã™ã€‚

```haskell
decode (ope, opd)
    = (case ope of
    GET    -> iGet
    PRINT  -> iPrint
    LOAD   -> iLoad
    STORE  -> iStore
    ADD    -> iAdd
    SUB    -> iSub
    GOTO   -> iGoto
    IFPOS  -> iIfpos
    IFZERO -> iIfzero
    STOP   -> iStop
    ) opd
```

## ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®ã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³

[ã‚«ãƒ¼ãƒ‹ãƒãƒ³å…ˆç”Ÿã®ã‚µã‚¤ãƒˆã«ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã®ã‚·ãƒŸãƒ¥ãƒ¬ãƒ¼ã‚¿ã®ãƒšãƒ¼ã‚¸](https://kernighan.com/toysim.html)ãŒã‚ã‚Šã€ãã“ã«æä¾›ã•ã‚Œã¦ã„ã‚‹ã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³ã®ã‚³ãƒ¼ãƒ‰ã¨æ„å‘³ãŒã¾ã¨ã‚ã‚‰ã‚Œã¦ã„ã¾ã™ã€‚

|ãƒ©ãƒ™ãƒ«|ã‚ªãƒšãƒ¬ãƒ¼ã‚¿^[ãƒˆã‚¤ãƒ»ã‚·ãƒŸãƒ¥ãƒ¬ãƒ¼ã‚¿ãƒ¼ã®ãƒšãƒ¼ã‚¸ã§ã¯ã‚ªãƒšãƒ¬ãƒ¼ã‚¿ã¯å°æ–‡å­—ã§è¡¨è¨˜ã—èª¬æ˜Žã•ã‚Œã¦ã„ã‚‹ãŒã€å®Ÿéš›ã¯ã‚ªãƒšãƒ¬ãƒ¼ã‚¿ã«ãŠã„ã¦ã‚‚ãƒ©ãƒ™ãƒ«ã«ãŠã„ã¦ã‚‚å¤§æ–‡å­—å°æ–‡å­—ã¯åŒºåˆ¥ã•ã‚Œã¦ã„ãªã„ã€‚]|ã‚ªãƒšãƒ©ãƒ³ãƒ‰|æ„å‘³|
|------|------|----|----|
||`GET`||ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã‹ã‚‰ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã«æ•°å€¤ã‚’èª­ã¿è¾¼ã‚€|
||`PRINT`||ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ã‚’å‡ºåŠ›æ¬„ã«è¡¨ç¤º|
||`LOAD` |*Val*|ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã«å€¤ï¼ˆæ•°å€¤ã¾ãŸã¯ãƒ¡ãƒ¢ãƒªç•ªåœ°ã®å†…å®¹ï¼‰ã‚’è¨­å®šï¼ˆå‘½ä»¤ä¸­ã®ã€Œå€¤ã€ã¯å¤‰åŒ–ã—ãªã„ï¼‰|
||`STORE` |*M*|ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ã®å†™ã—ã‚’ãƒ¡ãƒ¢ãƒªç•ªåœ°*M*ã«æ ¼ç´ï¼ˆã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ã¯å¤‰åŒ–ã—ãªã„ï¼‰|
||`ADD`|*Val*|ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ã«å€¤ï¼ˆæ•°å€¤ã¾ãŸã¯ãƒ¡ãƒ¢ãƒªç•ªåœ°ã®å†…å®¹ï¼‰ã‚’è¶³ã—è¾¼ã‚€ï¼ˆå‘½ä»¤ä¸­ã®ã€Œå€¤ã€ã¯å¤‰åŒ–ã—ãªã„ï¼‰|
||`SUB`|*Val*|ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ã‹ã‚‰å€¤ï¼ˆæ•°å€¤ã¾ãŸã¯ãƒ¡ãƒ¢ãƒªç•ªåœ°ã®å†…å®¹ï¼‰ã‚’å¼•ãï¼ˆå‘½ä»¤ä¸­ã®ã€Œå€¤ã€ã¯å¤‰åŒ–ã—ãªã„ï¼‰|
||`GOTO`|*L*|ãƒ©ãƒ™ãƒ«*L*ãŒä»˜ã‘ã‚‰ã‚ŒãŸå‘½ä»¤ã«é£›ã¶|
||`IFPOS`|*L*|ã‚‚ã—ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ãŒ$0$ã¾ãŸã¯æ­£ã§ã‚ã‚Œã°ãƒ©ãƒ™ãƒ«*L*ãŒä»˜ã‘ã‚‰ã‚ŒãŸå‘½ä»¤ã«é£›ã¶|
||`IFZERO`|*L*|ã‚‚ã—ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®å†…å®¹ãŒã¡ã‚‡ã†ã©$0$ã§ã‚ã‚Œã°ãƒ©ãƒ™ãƒ«*L*ãŒä»˜ã‘ã‚‰ã‚ŒãŸå‘½ä»¤ã«é£›ã¶|
||`STOP`||å®Ÿè¡Œã‚’åœæ­¢ã™ã‚‹|
|*M*|*Num*||ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’èµ·å‹•ã™ã‚‹å‰ã«ã€ã“ã®ãƒ¡ãƒ¢ãƒªä½ç½®ï¼ˆç•ªåœ°*M*ï¼‰ã‚’æ•°å€¤*Num*ã«è¨­å®šã™ã‚‹|

```haskell
iGet, iPrint, iStop
 , iLoad, iStore, iAdd, iSub
 , iGoto, iIfpos, iIfzero :: Operand -> Instruction

iGet   _ ((sz,mem),acc,ins,_)
    = ((sz, tail mem), read (head ins), tail ins, "")

iPrint _ ((sz,mem),acc,ins,_) 
    = ((sz, tail mem), acc, ins, show acc)

iLoad  opd ((sz,mem),_,ins,_)
    = ((sz, tail mem), value (sz,mem) opd, ins, "")

iStore opd (m,acc,ins,_)
    = next (update opd acc m, acc, ins, "")

iAdd opd ((sz,mem),acc,ins,_)
    = ((sz, tail mem), acc + value (sz, mem) opd, ins, "")
    
iSub opd ((sz,mem),acc,ins,_)
    = ((sz, tail mem), acc - value (sz, mem) opd, ins, "")

iGoto (Label l) ((sz,mem),acc,ins,_) 
    = ((sz, mem'), acc, ins, "")
    where
        mem' = dropWhile ((l /=) . fst) mem

iIfpos lab@(Label _) st@(_,acc,_,_) = st'
    where
        st' = if acc >= 0 then iGoto lab st else next st

iIfzero lab@(Label l) st@(_,acc,_,_) = st'
    where
        st' = if acc == 0 then iGoto lab st else next st

iStop  _ ((_,mem),acc,ins,_)
    = ((0, mem), acc, ins, "\nstopped")
```

`STOP`ã®å®Ÿè¡Œã«ã‚ˆã£ã¦ã€ãƒ¡ãƒ¢ãƒªã‚µã‚¤ã‚ºã‚’0ã«è¨­å®šã—ãŸã®ã§ã€åœæ­¢çŠ¶æ…‹ã®åˆ¤å®š`isFinal`ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«å®šç¾©ã§ãã¾ã™ã€‚

```haskell
isFinal st = case st of
    ((0,_),_,_,_) -> True
    _             -> False
```

ã„ãã¤ã‹ã®è£œåŠ©é–¢æ•°ã‚’å®šç¾©ã™ã‚Œã°ã€ã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³ã®å®šç¾©ã¯å®Œæˆã—ã¾ã™ã€‚

```haskell
next :: ToyState -> ToyState
next ((sz,mem),acc,ins,_) = ((sz, tail mem), acc, ins, "")

update :: Operand -> Data -> Memory -> Memory
update (Label m) val (sz,mem)
    = case break ((m ==) . fst) (take sz mem) of
          (xs, _:ys) -> (sz, cycle (xs ++ (m,Data val) : ys))
          _          -> error (m ++ ": not exist")

value :: Memory -> Operand -> Data
value (sz,mem) opd
    = case opd of
          Number num -> num
          Label lab  -> case dropWhile ((lab /=) . fst) (take sz mem) of
              (_,Data d):_ -> d
              _            -> error (lab ++ ": not exist")
```

## `ToyState` ã®è¡¨ç¤º

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ãŒå‹•ä½œã™ã‚‹æ§˜å­ã‚’è¦‹ã‚‹ãŸã‚ã«ã€`ToyState`ã‚’è¡¨ç¤ºã§ãã‚‹ã‚ˆã†ã«ã—ã¾ã—ã‚‡ã†ã€‚è¡¨ç¤ºã¯ãƒ¡ãƒ¢ãƒªã®è¡¨ç¤ºã¨ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿ã®è¡¨ç¤ºãŒã§ãã‚Œã°ã‚ˆã„ã§ã—ã‚‡ã†ã€‚

```haskell
showToyState :: ToyState -> String
showToyState ((sz, mem), acc, _, _)
    = unlines [showMemory (sz,mem), showAccumulator acc]

showMemory :: Memory -> String
showMemory (sz,mem)
    = unlines (map showCell (take sz mem))
    where
        showCell (lab, content)
            = leftJustify 8 lab ++ showContent content

leftJustify :: Int -> String -> String
leftJustify w s = take w (s ++ repeat ' ')

showContent :: Content -> String
showContent c = case c of
    Data d             -> show d
    Code (ope, opd) -> showOperator ope ++ ' ' : showOperand opd

showOperator :: Operator -> String
showOperator = show

showOperand :: Operand -> String
showOperand operand = case operand of
    None      -> ""
    Label lab -> lab
    Number n  -> show n

showAccumulator :: Accumulator -> String
showAccumulator acc = "Accumulator: " ++ show acc
```

## ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãƒ­ãƒ¼ãƒ€ãƒ¼

ã‚½ãƒ¼ã‚¹ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‹ã‚‰ã€ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®åˆæœŸçŠ¶æ…‹ã‚’æ§‹æˆã§ãã‚‹ã‚ˆã†ã«ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
loadProg :: String -> Memory
loadProg src = (,) . length <*> cycle . map conv $ lines $ map toUpper src

conv :: String -> (Label, Content)
conv line = case line of
    c:cs -> if isSpace c 
        then ("", toContent (words line))
        else case words line of
            label:content -> (label, toContent content)

toContent :: [String] -> Content
toContent ss = case ss of
    [cs] | all isDigit cs -> Data (read cs)
         | otherwise      -> Code (read cs, None)
    [ope,cs]              -> Code (read ope, opd)
        where
            opd = if all isDigit cs 
                then Number (read cs)
                else Label cs
```

## ãƒ©ãƒ™ãƒ«å‚ç…§ãƒã‚§ãƒƒã‚«ãƒ¼

ãƒˆã‚¤ãƒ»ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã§ã€å­˜åœ¨ã—ãªã„ãƒ©ãƒ™ãƒ«ã‚’å‚ç…§ã—ã¦ã„ãªã„ã‹ã©ã†ã‹ã€å®Ÿè¡Œå‰ã«ãƒã‚§ãƒƒã‚¯ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
labelCheck :: Memory -> Either Message Memory
labelCheck (sz, mem)
    = if null diffs then Right (sz, mem)
      else Left ("Not exist: " ++ unwords diffs)
    where
        (ls, cs)  = unzip (take sz mem)
        labs      = filter (not . null) ls
        rlabs     = mapMaybe exlab cs
        diffs     = map head (group (sort rlabs)) \\ labs
        exlab c    = case c of
            Code (_, Label l) -> Just l
            _                    -> Nothing

type Message = String
```

## ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ä¸Šã®ãƒˆã‚¤ãƒ»ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®èµ·å‹•

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ä¸Šã§ã®ãƒˆã‚¤ãƒ»ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®èµ·å‹•ã‚’ã‚„ã£ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```haskell
type SourceProgram = String

run :: SourceProgram -> Channel -> [Output]
run prog input
    = case labelCheck (loadProg prog) of
          Left msg  -> [msg]
          Right mem -> filter (not . null) (map output (executeProgram (initState mem input)))
    where
        output (_,_,_,out) = out

initState :: Memory -> Channel -> ToyState
initState mem input = (mem,0,input,"")
```

## ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ä¸Šã§ãƒˆã‚¤ãƒ»ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’èµ·å‹•ã™ã‚‹ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®èµ·å‹•

ãƒˆã‚¤ãƒ»ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’ãƒ•ã‚¡ã‚¤ãƒ«ã‹ã‚‰èª­ã‚€ã‚ˆã†ã«ã—ã€å¤–ç•Œã¨ã®å¯¾è©±ã§ãã‚‹ã‚ˆã†ã«ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
runToy :: FilePath -> IO ()
runToy prog = readFile prog >>= interact . drive . run

drive :: ([String] -> [String]) -> (String -> String)
drive w = unlines . w . lines
```

# ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®æ©Ÿæ¢°èªž

ã“ã‚Œã¾ã§æ‰±ã£ã¦ããŸã‚³ãƒ¼ãƒ‰ã¯ã€ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžã ã£ãŸã‚ã‘ã§ã™ãŒã€ã“ã‚Œã‚’æ•´æ•°ã«ç½®ãæ›ãˆã‚‹ã¨ã€æ©Ÿæ¢°èªžã«ãªã‚‹ã¨è€ƒãˆã¦ã‚ˆã„ã§ã—ã‚‡ã†ã€‚ã¾ãŸã€ãƒ¡ãƒ¢ãƒªã¯ãƒªã‚¹ãƒˆã§ã¯ãªãã€æ•´æ•°ã‚¢ãƒ‰ãƒ¬ã‚¹ã§ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã•ã‚Œã¦ã„ã‚‹é…åˆ—ã§ã‚ã‚‰ã‚ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
type Mem = A.Array Addr Int
type Addr = Int
```

## ã‚ªãƒšã‚³ãƒ¼ãƒ‰ã€ã‚ªãƒšãƒ©ãƒ³ãƒ‰ã€ãƒ‡ãƒ¼ã‚¿

ã‚ªãƒšã‚³ãƒ¼ãƒ‰ã¯ã€ã‚ªãƒšãƒ¬ãƒ¼ã‚¿ç•ªå·ã§è¡¨ç¾ã—ã¾ã™ã€‚

```haskell
fromOperator :: Operator -> MOpecode
fromOperator = fromEnum

toOperator :: MOpecode -> Operator
toOperator = toEnum
```

ã‚ªãƒšãƒ©ãƒ³ãƒ‰ã¯ã€å³å€¤ã¾ãŸã¯é–“æŽ¥å‚ç…§å€¤ã§ã™ã€‚å¶æ•°ã§å³å€¤ã€å¥‡æ•°ã§é–“æŽ¥å‚ç…§å€¤ã‚’è¡¨ç¾ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
type MOperand = Int

fromImm :: Int  -> MOperand
fromImm imm = 2 * imm

toImm :: MOperand -> Int
toImm opd = opd `div` 2

fromInd :: Addr -> MOperand
fromInd ind = 2 * ind + 1

toInd :: MOperand -> Addr
toInd opd = opd `div` 2
```

ãƒ‡ãƒ¼ã‚¿ã«ã¤ã„ã¦ã¯ã€ãã®ã¾ã¾`Int`ã§è¡¨ç¾ã—ã¾ã™ã€‚

## ã‚¢ã‚»ãƒ³ãƒ–ãƒ«

ãƒˆã‚¤ãƒ»ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ãƒ¼ã®ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžã§è¡¨ç¾ã—ãŸãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’æ©Ÿæ¢°èªžã§è¡¨ç¾ã—ãŸãƒ—ãƒ­ã‚°ãƒ©ãƒ ã«å¤‰æ›ã™ã‚‹ã‚¢ã‚»ãƒ³ãƒ–ãƒ©ã‚’æ›¸ãã¾ã—ã‚‡ã†ã€‚

```haskell
assemble :: SourceProgram -> Mem
assemble = asm . loadProg
```

`asm :: Memory -> Mem` ã¯ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®å†…éƒ¨è¡¨ç¾ã‹ã‚‰æ©Ÿæ¢°èªžãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®å†…éƒ¨è¡¨ç¾ã¸ã®å¤‰æ›é–¢æ•°ã§ã™ã€‚

```haskell
asm :: Memory -> Mem
asm (sz, mem) = A.listArray (0, n - 1) (concat mmem)
    where
        ((n, tab), mmem) = mapAccumL psi (0, []) mem'
        mem' = take sz mem
        psi (i,syms) (lab, content) = case content of
            Code (ope, opd) -> case opd of
                None            -> ((i', syms'), [fromEnum ope])
                Number v        -> ((i'', syms'), [fromEnum ope, fromImm v])
                Label l         -> ((i'', syms'), [fromEnum ope, fromInd a])
                    where
                        a = snd (head (dropWhile ((l /=) . fst) tab))
            Data d          -> ((i', syms'), [d])
            where
                i'  = succ i
                i'' = succ i'
                syms' = case lab of
                    "" -> syms
                    _  -> (lab, i) : syms
```

## ãƒˆã‚¤ãƒ»ãƒžã‚·ãƒ³ã®çŠ¶æ…‹

ã‚ã‚‰ãŸã‚ã¦ã€ã“ã®ãƒˆã‚¤ãƒ»ãƒžã‚·ãƒ³ã®çŠ¶æ…‹ã‚’æ§‹æˆã—ã¾ã—ã‚‡ã†ã€‚ãƒ¡ãƒ¢ãƒª`Mem`ãŒå«ã¾ã‚Œã¦ã„ã‚‹ã¯ãšã§ã™ã­ã€‚ãã‚Œã‹ã‚‰ã€ã‚¢ã‚­ãƒ¥ãƒ ãƒ¬ãƒ¼ã‚¿`AC`ã¨ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚«ã‚¦ãƒ³ã‚¿ãƒ¼`PC`ã¨ã„ã†2ã¤ã®ãƒ¬ã‚¸ã‚¹ã‚¿ãŒã‚ã‚‹ã§ã—ã‚‡ã†ã€‚ã¾ãŸã€å…¥åŠ›ãƒãƒ£ãƒ³ãƒãƒ«`Chan`ã€å‡ºåŠ›`Out`ãŒã‚ã‚‹ã¯ãšã§ã™ã€‚

```haskell
type AC = Register
type PC = Register
type Register = Int
type Chan = [Int]
type Out  = Int
none :: Out
none = minBound
type MState = (Mem, AC, PC, Chan, Out)

mIsFinal :: MState -> Bool
mIsFinal (_,_,pc,_,_) = pc < 0

mOut :: MState -> Out
mOut (_,_,_,_,out) = out
```

çŠ¶æ…‹ãŒå®šç¾©ã§ããŸã‚‰ã€æ©Ÿæ¢°èªžã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’å‹•ä½œã•ã›ã‚‹ã“ã¨ã‚’è€ƒãˆã¾ã™ã€‚ã¨ã„ã£ã¦ã‚‚ã€ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã§ã‚„ã£ãŸã®ã¨åŒã˜ã“ã¨ã‚’ã™ã‚‹ã ã‘ã§ã™ã€‚

### ãƒ•ã‚§ãƒƒãƒ-ãƒ‡ã‚³ãƒ¼ãƒ‰-å®Ÿè¡Œã‚µã‚¤ã‚¯ãƒ«

ãƒ•ã‚§ãƒƒãƒâ€”ãƒ‡ã‚³ãƒ¼ãƒ‰-å®Ÿè¡Œã®ã‚µã‚¤ã‚¯ãƒ«ã‚’è€ƒãˆã¾ã—ã‚‡ã†ã€‚

ã¾ãš`mFetch`ã¯ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚«ã‚¦ãƒ³ã‚¿ãƒ¼ãŒæŒ‡ã™ä½ç½®ã®ã‚ªãƒšã‚³ãƒ¼ãƒ‰ã‚’å–å¾—ã—ã¾ã™ã€‚

```haskell
type MOpecode = Int
type MInstruction = MState -> MState

mFetch   :: MState -> MOpecode
mFetch (mem,_,pc,_,_) = mem A.! pc
```

æ¬¡ã«ãƒ‡ã‚³ãƒ¼ãƒ‰`mDecode`ã¯ãƒ‡ã‚£ã‚¹ãƒ‘ãƒƒãƒã§ã™ã­ã€‚

```haskell
mDecode :: MOpecode -> MInstruction
mDecode ope (mem,ac,pc,ins,_) = case toEnum ope of
    GET    -> (mem, head ins, pc + 1, tail ins, none)
    PRINT  -> (mem, ac, pc + 1, ins, ac)
    STOP   -> (mem, ac, -1, ins, none)
    LOAD   -> (mem, getval mem (pc + 1), pc + 2, ins, none)
    STORE  -> (mem A.// [(addr (mem A.! (pc + 1)), ac)], ac, pc + 2, ins, none)
    ADD    -> (mem, ac + getval mem (pc + 1), pc + 2, ins, none)
    SUB    -> (mem, ac - getval mem (pc + 1), pc + 2, ins, none)
    GOTO   -> (mem, ac, pc', ins, none)
                where
                    pc' = addr (mem A.! (pc + 1))
    IFPOS  -> (mem, ac, pc', ins, none)
                where
                    pc' = if ac >= 0 then addr (mem A.! (pc + 1)) else pc + 2
    IFZERO -> (mem, ac, pc', ins, none)
                where
                    pc' = if ac == 0 then addr (mem A.! (pc + 1)) else pc + 2
```

è£œåŠ©é–¢æ•°`getval :: Mem -> Addr -> Int`ã¯ãƒ¡ãƒ¢ãƒªã®æŒ‡å®šã—ãŸã‚¢ãƒ‰ãƒ¬ã‚¹ã«ã‚ã‚‹ã‚ªãƒšãƒ©ãƒ³ãƒ‰ãŒç¤ºã™å€¤ã§ã™ã€‚

```haskell
getval :: Mem -> Addr -> Int
getval mem a = case mem A.! a of
    opd -> if even opd then val opd
           else mem A.! addr opd

addr :: MOperand -> Addr
addr = toInd

val :: MOperand -> Int
val = toImm
```

æœ€å¾Œã«å®Ÿè¡Œã¯ã€ãƒ‡ã‚³ãƒ¼ãƒ‰å¾—ã‚‰ã‚ŒãŸã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³ã§ã‚ã‚‹çŠ¶æ…‹é·ç§»é–¢æ•°ã®é©ç”¨ã§ã™ã®ã§ã€ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžã®ã¨ãã¨åŒã˜ã§`id`ã§ã„ã„ã§ã—ã‚‡ã†ã€‚

```haskell
mExecute :: MInstruction -> MState -> MState
mExecute = id
```

1ã¤ã®ã‚¤ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚·ãƒ§ãƒ³ã‚’å®Ÿè¡Œã™ã‚‹éŽç¨‹ï¼ˆã¤ã¾ã‚Šã€ãƒ•ã‚§ãƒƒãƒ-ãƒ‡ã‚³ãƒ¼ãƒ‰-å®Ÿè¡Œã®1ã‚µã‚¤ã‚¯ãƒ«åˆ†ï¼‰ã‚’`mStep`ã§è¡¨ç¾ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
mStep    :: MState -> MState
mStep st = mExecute (mDecode (mFetch st)) st
```

## æ©Ÿæ¢°èªžãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®å®Ÿè¡Œ

å®Ÿè¡Œã«é–¢ã—ã¦ã‚‚ã‚¢ã‚»ãƒ³ãƒ–ãƒªè¨€èªžã®ã¨ãã¨åŒã˜ã§ã™ã­ã€‚

```haskell
mExecuteProgram :: MState -> [MState]
mExecuteProgram state = state : rests
    where
        rests | mIsFinal state = []
              | otherwise      = mExecuteProgram (mStep state)
```

## æ©Ÿæ¢°èªžãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®èµ·å‹•

æ©Ÿæ¢°èªžãƒ—ãƒ­ã‚°ãƒ©ãƒ ã®èµ·å‹•

```haskell
mRun :: Mem -> Chan -> [Out]
mRun mem chan = foldr phi [] (mExecuteProgram (mem, 0, 0, chan, none))
    where
        phi (_,_,_,_,o) ys = if minBound == o then ys else o : ys
```